LABTITLE=: 'Scientific Units'

NB. =========================================================
Lab Section INTRO
UU is an addon to convert quantities between different 
units of measurement. 
It is based on the International System of Units 
(Le Système International d'Unités) or "SI".
   http://en.wikipedia.org/wiki/SI_units

But it knows some other units too. 
You can even define your own, by editing "UU-constants":
   open '~addons/math/uu/uuc.ijs'
)

require 'dev/uu'

uu '3 ft'
uu '1 yd'
uu '1 min'

NB. =========================================================
Lab Section CONVERT
Monadic uu will convert to the fundamental units of SI:
   metre [m], kilogram [kg], second [s], Ampere [A],
   Kelvin [K], candela [cd], mole [mol].

But if you specify target units as the left argument,
uu will convert to those units instead...
)

'ft' uu '1 yd'
'yd' uu '6 ft'
'km' uu '56789 m'

NB. =========================================================
Lab Section COMPATIBLE
...But of course only if the units are compatible.
)

'km' uu '100 s'


NB. =========================================================
Lab Section COMPATIBLE2
The Joule [J] is the SI unit of energy.

How do you find out which units are compatible with [J]?

Energy units all share the same expansion as basic units:
   [kg m^2/s^2], or [kg m²/s²]

You could dream up various units to test one-by-one...
)

uu '1 J'
uu '1 erg'
uu '1 eV'
uu '1 cal'
uu '1 Mton.TNT'
uu '1 kW'  NB. No: kilowatt is a unit of power, not energy

NB. =========================================================
Lab Section COMPATIBLE3
But locale _uu_ has a verb to help you: compatible_uu_
)

'cal' compatible_uu_ 'J'
'J' compatible_uu_ 'cal'
'J' compatible_uu_ 'kW'	NB. Kilowatt is a unit of power, not energy
'J' compatible_uu_ 'kWh'

NB. =========================================================
Lab Section COMPATLIST
You can even ask _uu_ to tell you all the units it knows
which are compatible with [J]...
)

compatlist_uu_ 'J'

NB. =========================================================
Lab Section COMPATLIST
But if _uu_ doesn't know the units you give it 
(or you've misspelt them) then it will reply with 0...
)

'J' compatible_uu_ 'oodles'

   NB. NOTE the behavior for: unknown units...
uu '1 oodles'

uu 1 ; 'oodles'

NB. =========================================================
Lab Section ARITHMETIC
Giving uu a literal right-argument is just a convenience 
for use in "calculator mode".

When you use uu in a program, you will call it with
a right argument of the form:

	number ; units

--where: (units) -is a single char, or a string.

uu responds in the same format...
)

uu 1 ; 'r.p.m'

'mi/h' uu  88 ; 'ft/sec'

NB. =========================================================
Lab Section SLASH1
'/' is the _uu_ convention for DIMENSIONLESS UNITS.

This prevents the units string vanishing when you divide
compatible units by each other.)

Dimensionless units are mostly ratios:
   sin, cos, specific gravities, %, etc.
)

uu '1 km/mi'		NB. conversion factor: km --> miles
'%' uu '1 km/mi'	NB. ditto, as a percentage

NB. =========================================================
Lab Section SLASH2
To "divide" two arbitrary units strings, use: udiv_uu_

(Don't just put '/' between them, unless you know for certain
that the denominator doesn't contain slash).

Then use: uu -to cancel-out...
)

u1=: 'kg m^2/A^2/s^3'		NB. energy
u2=: 'kg m^2/A^2/s^2'		NB. power
u1 udiv_uu_ u2
uu  1 ; u1 udiv_uu_ u2

NB. =========================================================
Lab Section SI_LOOK1
uu approximates the "SI look-and-feel" with unicode chars
(in utf-8 encoding, the standard for the J session).

To suppress this behavior, enter: uunicode 0

If you enter: uunicode 1
-then uu will not only output utf-8 chars,
but accept them as input too.
)

'Ohm' uu '1 kg m^2/A^2/s^3'
      uu '1 kg m^2/A^2/s^3'

uunicode 0
'Ohm' uu '1 kg m^2/A^2/s^3'
      uu '1 kg m^2/A^2/s^3'

uunicode 1	NB. reset to the default

NB. =========================================================
Lab Section SI_LOOK2
Strict SI units do not use '/' to represent units quotients.
Thus uu accepts [m/s/s] and [m/s^2] (units of acceleration)
whereas SI insists on the format [m s⁻²].

You can force UU to output units in the strict SI convention
by setting: uunicode 2
)

uunicode 0
   uu '1 J'

uunicode 2
   uu '1 J'

uunicode 1	NB. reset to the default


NB. =========================================================
Lab Section ZEPTO1
"SI" allows you to attach a scaling prefix to units.

Most people know about kilo [k-], mega [M-], milli [m-]
and micro [mu-] or [µ]. But there are others. 
The full range is:

 	deca- 	hecto- 	kilo- 	mega- 	giga-
 	da 	h 	k 	M 	G
 	10^1 	10^2 	10^3 	10^6 	10^9
	--------------------------------------
 	tera- 	peta- 	exa- 	zetta- 	yotta-
 	T 	P 	E 	Z 	Y
 	10^12 	10^15 	10^18 	10^21 	10^24
	--------------------------------------
 	deci- 	centi- 	milli- 	micro- 	nano-
 	d 	c 	m 	µ [mu]	n
 	10^−1 	10^−2 	10^−3 	10^−6 	10^−9
	--------------------------------------
 	pico- 	femto- 	atto- 	zepto- 	yocto-
 	p 	f 	a 	z 	y
 	10^−12 	10^−15 	10^−18 	10^−21 	10^−24
)

uu '1 GW'	NB. one gigawatt
uu '1e21 zg'	NB. one gram, SI-style
uu '1 g'	NB. -for comparison.
   NB. ...SI takes [kg] not [g] as the basic unit of mass.

NB. =========================================================
Lab Section MICRON
The term "micron" and the symbol µ, representing the
micrometre, were officially accepted between 1879 and 1967, 
but officially revoked by the ISI in 1967.
   http://en.wikipedia.org/wiki/Micron

However _uu_ accepts [µ] by itself to mean "micron".

WARNING: not every unicode variant of µ will work!

Use either 'mu' or 'µ' for entering microns [µm]...
)

uu '100 mum'	NB. 100 microns
uu '100 µm'
uu '100 mu'
uu '100 µ'
'Ang' uu '1 µm'	NB. microns --> Ångstrom units
'Å' uu '1 µ'
'µ' uu '1 Å'

NB. =========================================================
Lab Section ZEPTO2
uu accepts scaling prefixes on some non-SI units too.

Why?

Because it's too much trouble to forbid them.
)

uu '1 zft'	NB. zepto-feet -need very small shoes.
   NB. Earth mass, in zetta-elephants...
'Zelephant' uu '1 ea.mass'
   NB. Earth mass, in yotta-elephants...
SCI_uu_=: 10	NB. force exponential numerals above 10
SIG_uu_=: 5	NB. decimal places to show
'Yelephant' uu '1 ea.mass'
   NB. Restore recommended settings...
SCI_uu_=: 1e5 [ SIG_uu_=: 3

NB. =========================================================
Lab Section MISC
Miscellaneous conversions...
)

   NB. yottagrams -in Imperial tons. Heavy stuff...
'ton' uu '1 Yg'
   NB. ditto, in metric tons (tonnes)...
't' uu '1000 Zg'
   NB. Weight of the Space shuttle -in African elephants...
'elephant' uu '2030 t'
   NB. Mass of the moon in terms of earth...
'ea.mass' uu '1 moon.mass'
   NB. Moon gravity, in units of earth-gravity...
'grav' uu '1 moon.g'
